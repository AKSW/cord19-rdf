PREFIX covid: <http://covid19.aksw.org/resource/>


# Create resources for the cited documents:
# <thisdocument-citation-[12]>
#   a Citation ; of someWork [ havingAuthors (x y z) ]

INSERT {
  GRAPH ?g { ?s eg:authorJson ?a }
}
WHERE
{
  GRAPH ?g { ?s eg:json ?json }
  BIND(json:path(?json, "$.metadata.authors") AS ?a)
}



INSERT {
  GRAPH ?g {
    ?s eg:hasCitation ?c .

    ?c
      a eg:Citation ;
      eg:authorJson ?a ;
      .
  }
}
WHERE {
  GRAPH ?g { ?s eg:json ?json }

  # "bib_entries": {
  #          "BIBREF0": {
  #              "ref_id": <str>, ... }
  BIND(json:path(json:entries(json:path(?json, "$.bib_entries")), "$[*].value") AS ?list)

  ?list json:unnest (?item ?i)
  BIND(json:path(?item, "$.ref_id") AS ?refId)
  BIND(IRI(CONCAT(STR(?s), '-citation-', ?refId)) AS ?c)
  BIND(json:path(?item, "$.authors") AS ?a)

}




INSERT {
  GRAPH ?g {
    ?s
      eg:hasAuthor ?a ;
      eg:authorList ?al ;
      .
    
    ?al
      a rdf:Seq ;
      ?member ?a ;
      .

    ?a
      a foaf:Person ;
      rdfs:label ?n ; # Also use the name as the label
      foaf:firstName ?fn ;
      eg:middleName ?mn ;
      foaf:lastName ?ln ;
      foaf:name ?n ;
      foaf:mbox ?mbox ;
      eg:suffix ?suffix ;
      eg:affiliationJson ?aj ;
      .
  }
}
WHERE
{
  GRAPH ?g { ?s eg:authorJson ?json }

  ?json json:unnest (?item ?i)
  BIND(json:path(?item, "$.first") AS ?fn)
  BIND(json:js("function(x) { return x.middle.join(' '); }", ?item) AS ?mn)
  BIND(json:path(?item, "$.last") AS ?ln)
  BIND(json:path(?item, "$.email") AS ?mbox)
  BIND(json:path(?item, "$.suffix") AS ?suffix)
  BIND(json:path(?item, "$.affiliation") AS ?aj)

  BIND(REPLACE(CONCAT(?fn, ' ', ?mn, ' ', ?ln), "\\s+", " ") AS ?n)
  BIND(IRI(CONCAT(STR(?s) , '-author-', ENCODE_FOR_URI(?n))) AS ?a)

  BIND(IRI(CONCAT(STR(?s) , '-authors')) AS ?al)
  BIND(IRI(CONCAT(STR(rdf:), '_', STR(?i + 1))) AS ?member)
}


# Add some known mappings
#INSERT DATA {
#  eg:institution eg:predicateLabel "institution" .
#  eg:Institution eg:classLabel "institution" .
#
#}

INSERT {
  GRAPH ?g {
    ?s
      eg:affiliation ?a ;
      .

    ?a
      eg:laboratory ?lab ;
      eg:institution ?ins ;
      eg:location ?l ;
     .

    ?l
      eg:addrLine ?loc_addr ;
      eg:postCode ?loc_post ;
      eg:settlemenet ?loc_sett ;
      eg:country ?loc_coun ;
      .
  }  
}
WHERE
{
  GRAPH ?g { ?s eg:affiliationJson ?json }
  BIND(json:path(?json, '$.laboratory') AS ?lab)
  BIND(json:path(?json, '$.institution') AS ?ins)

  BIND(REPLACE(CONCAT(COALESCE(?ins, ""), "-", COALESCE(?lab, "")), "(^-+)|-+$", "") AS ?affId)

  BIND(json:path(?json, '$.location.addrLine') AS ?loc_addr)
  BIND(json:path(?json, '$.location.postCode') AS ?loc_post)
  BIND(json:path(?json, '$.location.settlement') AS ?loc_sett)
  BIND(json:path(?json, '$.location.country') AS ?loc_coun)
  BIND(REPLACE(CONCAT(COALESCE(?loc_addr, ""), "-", COALESCE(?loc_post, ""), "-", COALESCE(?loc_sett, ""), "-", COALESCE(?loc_coun, "")), "(^-+)|-+$", "") AS ?tmpLocId)

  BIND(IF(?tmpLocId = "", 1 < 'a', ?tmpLocId) AS ?locId)

  BIND(IRI(CONCAT(STR(covid:), "affiliation-", ENCODE_FOR_URI(?affId), "-", ENCODE_FOR_URI(?locId))) AS ?a)
  BIND(IRI(CONCAT(STR(covid:), "location-", ENCODE_FOR_URI(?locId))) AS ?l)
}


