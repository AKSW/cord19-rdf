PREFIX cord: <http://cord19.aksw.org/resource/>
PREFIX nif: <http://persistence.uni-leipzig.org/nlp2rdf/ontologies/nif-core#>
PREFIX its: <http://www.w3.org/2005/11/its/rdf#>

INSERT {
  GRAPH ?g {
    ?s eg:hasText ?a .

    ?a
      rdfs:label ?text ;
      eg:section ?section ;
      eg:textHash ?textHash ;
      eg:bodyJson ?item ;
      .
  }
}
WHERE
{
  GRAPH ?g { ?s eg:json ?json }
  VALUES ?jsonKey { "$.abstract" "$.body_text" }
  BIND(json:path(?json, ?jsonKey) AS ?list)
  ?list json:unnest (?item ?i)

  BIND(json:path(?item, "$.text") AS ?text)
  BIND(SHA256(?text) AS ?textHash)

  BIND(json:path(?item, "$.section") AS ?section)
  BIND(IRI(CONCAT(STR(cord:), 'text-', ?textHash)) AS ?a)
}



# cite spans
INSERT {
  GRAPH ?g {
    ?body
      eg:annotation ?s ;
      .

    ?s
      a eg:Annotation ;
      its:taClassRef ?annotationType ;
      nif:beginIndex ?start ;
      nif:endIndex   ?end ;
      nif:anchorOf   ?text ;
      eg:refId       ?refId ;
      .
  }
}
WHERE
{
  GRAPH ?g { ?body eg:bodyJson ?json }
  VALUES (?jsonPath ?annotationType) { ("$.cite_spans" eg:CiteSpan) ( "$.ref_spans" eg:RefSpan ) }
  BIND(json:path(?json, ?jsonPath) AS ?list)
  ?list json:unnest (?item ?i)

  BIND(xsd:long(json:path(?item, "$.start")) AS ?start)
  BIND(xsd:long(json:path(?item, "$.end")) AS ?end)
  BIND(json:path(?item, "$.text") AS ?text)
#  BIND(json:path(?item, "$.ref_id") AS ?refId)

  BIND(IRI(CONCAT(STR(?body), '#char', STR(?start), ',', STR(?end))) AS ?s)
}








DELETE { GRAPH ?g { ?s eg:bodyJson ?o } } WHERE { GRAPH ?g { ?s eg:bodyJson ?o } }
DELETE { GRAPH ?g { ?s eg:json ?o } } WHERE { GRAPH ?g { ?s eg:json ?o } }

CONSTRUCT WHERE { GRAPH ?g { ?s ?p ?o } }

